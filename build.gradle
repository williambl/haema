plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "FabricMC"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = "Sonatype"
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name = "willBlRepositoryReleases"
        url = "https://maven.willbl.dev/releases"
    }
    maven {
        name = "Jamies"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name = "Jared"
        url 'https://maven.blamejared.com'
        content {
            includeGroup "vazkii.patchouli"
        }
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases/'
        content {
            includeGroup "io.github.ladysnake"
            includeGroupByRegex "io\\.github\\.onyxstudios.*"
            includeGroupByRegex "dev\\.onyxstudios.*"
        }
    }
    maven {
        name = "GeckoLib"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup "software.bernie.geckolib"
        }
    }
    maven {
        name = "Cotton"
        url = "https://server.bbkr.space/artifactory/libs-release/"
        content {
            includeGroup "io.github.cottonmc"
        }
    }
    maven {
        name = "Shedaniel"
        url = "https://maven.shedaniel.me"
        content {
            includeGroupByRegex "dev\\.architectury.*"
            includeGroupByRegex "me\\.shedaniel.*"
            includeGroupByRegex "me\\.sargunvohra.*"
        }
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/releases/"
        content {
            includeGroup "io.github.prospector"
            includeGroup "com.terraformersmc"
            includeGroup "dev.emi"
        }
    }
    maven {
        name = "Architectury"
        url "https://maven.architectury.dev/"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        name = "cafeteria.dev"
        url = 'https://maven.cafeteria.dev'
        content {
            includeGroup 'net.adriantodt.fabricmc'
        }
    }
    maven {
        name = "Flemmli97"
        url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
        content {
            includeGroup 'io.github.flemmli97'
        }
    }
    maven {
        name = "Gunpowder"
        url = "https://maven.martmists.com/releases"
        content {
            includeGroup 'io.github.gunpowder'
        }
    }
    maven {
        name = "Permissions"
        url = "https://maven.nucleoid.xyz"
        content {
            includeGroup 'eu.pb4'
        }
    }
    maven {
        name = "HavenKing"
        url = "https://hephaestus.dev/release"
        content {
            includeGroup 'dev.inkwell'
        }
    }
    maven {
        name = "Flemmli97"
        url = "https://gitlab.com/api/v4/projects/21830712/packages/maven"
        content {
            includeGroup 'io.github.flemmli97'
        }
    }
    maven {
        name = "Appleskin"
        url = "https://maven.ryanliptak.com/"
        content {
            includeGroup 'squeek.appleskin'
        }
    }
    maven {
        name = "Nucleoid"
        url = "https://maven.nucleoid.xyz"
        content {
            includeGroup 'xyz.nucleoid'
        }
    }
    maven {
        name = "SmartBrainLib (SBL) Maven Repo"
        url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
        content {
            includeGroup "net.tslat.smartbrainlib"
        }
    }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "JitPack"
        url 'https://jitpack.io'
    }

    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    ///
    /// HARD DEPS
    ///

    // Fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
    }

    ///
    /// INCLUDED
    ///

    // Apoli
    modCompileOnly ("com.github.apace100:apoli:${project.apoli_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Cardinal Components
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cc_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cc_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Data Functions
    modImplementation include("com.williambl.dfunc:data-functions:${project.dfunc_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Reach Entity Attributes
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Satin
    modImplementation include("io.github.ladysnake:satin:${project.satin_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // More codecs
    modImplementation include("xyz.nucleoid:more-codecs:${project.morecodecs_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Smart Brain Lib
    modImplementation include("net.tslat.smartbrainlib:SmartBrainLib-fabric-${project.minecraft_version}:${project.sbl_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modImplementation include("net.tslat.smartbrainlib:SmartBrainLib-common-${project.minecraft_version}:${project.sbl_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        modid {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=haema"

            runDir "build/datagen"
        }
    }

    accessWidenerPath = file("src/main/resources/haema.accesswidener")
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}


processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "willBlRepositoryReleases"
            url = "https://maven.willbl.dev/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

curseforge {
    apiKey = project.hasProperty('curseforge_api_key') ? project.property('curseforge_api_key') : ''
    //noinspection GroovyAssignabilityCheck
    project {
        id = project.curseforge_project_id
        changelogType = 'markdown'
        changelog = file("changelog.md")

        addGameVersion project.minecraft_version
        addGameVersion "Fabric"
        addGameVersion "Quilt"

        mainArtifact(remapJar) {
            displayName = "v${project.version.toString().split("\\+")[0]} for Fabric/Quilt&MC $project.minecraft_version"
            releaseType = "release"
            relations {
                requiredDependency "fabric-api"
                requiredDependency "fabric-language-kotlin"
                requiredDependency "geckolib"
                embeddedLibrary "cardinal-components"
                embeddedLibrary "patchouli-fabric"
                embeddedLibrary "satin-api"
                optionalDependency "bewitchment"
                optionalDependency "emi"
                optionalDependency "origins"
                optionalDependency "rats-mischief"
            }
        }
    }
}

modrinth {
    token = project.hasProperty("modrinth_api_key") ? project.property("modrinth_api_key") : ""
    projectId = project.modrinth_project_id
    versionNumber = project.version
    changelog = file("changelog.md").text
    versionType = "release"
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["fabric", "quilt"]
    dependencies {
        // The scope can be `required`, `optional`, `incompatible`, or `embedded`
        // The type can either be `project` or `version`
        required.project "fabric-api"
        required.project "fabric-language-kotlin"
        required.project "geckolib"
        embedded.project "cardinal-components-api"
        embedded.project "patchouli"
        embedded.project "satin-api"
    }
}
